/*     ______            __                                      *\
**    / ____/_  __ ___  / /     FunL Programming Language        **
**   / __/ / / / / __ \/ /      (c) 2014, Edward A. Maxedon, Sr. **
**  / /   / /_/ / / / / /__     http://funl-lang.org/            **
** /_/    \____/_/ /_/____/                                      **
\*                                                               */

function funl.modules.Predef.{
	print, println, printf, readLine,
	error, require, assert,
	array, seq, list, dict, set, tuple,
	int
	}

native java.lang.System.gc

def
	compose( f, g ) = x -> f( g(x) )

	fst( (a, _) ) = a

	snd( (_, b) ) = b

	head( x:_ )                 = x

	tail( _:xs )                = xs
	
	map( f, [] )                = []
	map( f, x:xs::Stream )      = f( x ) # map( f, xs )
	map( f, x:xs )              = f( x ) : map( f, xs )
	map( f, (a::Array|a::Seq) ) = map( f, list(a) )

	filter( p, [] )                       = []
	filter( p, x:xs::Stream	) | p( x )   = x # filter( p, xs )
	filter( p, x:xs	) | p( x )           = x : filter( p, xs )
	filter( p, _:xs	)                    = filter( p, xs )
	filter( p, (a::Array|a::Seq) )        = filter( p, list(a) )

	foldl( f, z, [] )                 = z
	foldl( f, z, x:xs )               = foldl( f, f(z, x), xs )
	foldl( f, z, (a::Array|a::Seq) )  = foldl( f, z, list(a) )
	foldl( f, z, it::Iterator )       =
		def iterfold( z ) =
			if it.hasNext()
				iterfold( f(z, it.next()) )
			else
				z

		iterfold( z )

	foldl1( f, x:xs )               = foldl( f, x, xs )
	foldl1( _, [] )                 = error( "foldl1: empty list" )
	foldl1( f, (a::Array|a::Seq) )  = foldl1( f, list(a) )
	foldl1( f, it::Iterator )
		| it.hasNext()                = foldl( f, it.next(), it )
		| otherwise                   = error( "foldl1: empty iterator" )

	sum( l )									= foldl( (a, b) -> a + b, 0, l )
	product( l )							= foldl( (a, b) -> a*b, 1, l )

	max( a, b )							= if a <= b then b else a
	max( xs )								= foldl1( max, xs )

	maxBy( f, xs )           = foldl1( (a, b) -> if f(a) <= f(b) then b else a, xs )
	
	min( a, b )							= if a >= b then b else a
	min( xs )								= foldl1( min, xs )