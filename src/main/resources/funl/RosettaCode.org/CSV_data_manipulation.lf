=={{header|FunL}}==
<lang funl>~~import io.{lines, PrintWriter}

data Table( header, rows )

def read( file ) =
  l = lines( file )
  
  def next = vector( l.next().split(',') )
  
  if l.isEmpty() then
    return Table( vector(), [] )
  
  header = next()
  rows = seq()
  
  while l.hasNext()
    rows += next()
    
  Table( header, rows.toList() )
  
def write( table, out ) =
  w = if out is String then PrintWriter( out ) else out

  w.println( table.header.mkString(',') )
  
  for r <- table.rows
    w.println( r.mkString(',') )
  
  if out is String
    w.close()
  
def updateRow( header, row, updates ) =
  r = dict( (header(i), row(i)) | i <- 0:header.length() )
  updates( r )
  vector( r(f) | f <- header )

def update( table, updates ) =		
  Table( table.header, (updateRow(table.header, r, updates) | r <- table.rows).toList() )
  
def addColumn( table, column, updates ) =
  Table( table.header + [column], (updateRow(table.header + [column], r + [null], updates) | r <- table.rows).toList() )

t = addColumn( read('test.csv'), 'SUM', r -> r('SUM') = sum(int(v) | (_, v) <- r if v != null) )
write( t, 'test_out.csv' )
write( t, System.out )~~</lang>

{{out}}

<pre>
C1,C2,C3,C4,C5,SUM
1,5,9,13,17,45
2,6,10,14,18,50
3,7,11,15,19,55
4,8,12,16,20,60
</pre>
